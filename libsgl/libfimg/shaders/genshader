#!/bin/gawk -f

function compile(f, t)
{
	if (f != "") {
		close(f)
		system("wine orion.exe -a -" t " " f)
		system("rm -f " f " " f ".h ")
		print "static const unsigned int " f "[] = {" >> file ".h"
		system("hexdump -s 72 -v -e '1 \"\t0x%08x, 0x%08x, 0x%08x, 0x%08x,\"' -e '\"\n\"' " f ".bin >> " file ".h")
		print "};" >> file ".h"
		print "" >> file ".h"
		system("rm -f " f ".bin")
	}
}

function remove(f)
{
	if (f != "") {
		system("rm -f " f)
	}
}

function copy(s, d)
{
	if (s != "" && d != "") {
		system("cp " s " " d)
	}
}

BEGIN {
	blkfile = ""
	cmnfile = ""
	common = 1
}

FNR == 1 {
	compile(blkfile, type)
	if(blkfile != "") {
		print "#endif" >> file ".h"
	}

	common = 1

	split(FILENAME, tmp, ".")
	file = tmp[1]
	delete tmp

	cmnfile = file ".common"
	remove(cmnfile)

	remove(file ".h")
	print "#ifndef _" toupper(file) "_H_" >> file ".h"
	print "#define _" toupper(file) "_H_" >> file ".h"
	print "" >> file ".h"
}

/^%.*$/ {
	common = 0

	compile(blkfile, type)

	if ($2 == "" || $3 == "") {
		print "Syntax error at line " FNR " in file " FILENAME
		exit 1
	}

	if ($2 != "f" && $2 != "v") {
		print "Invalid shader type at line " FNR " in file " FILENAME
		print "Valid shader types are 'f' and 'v'"
		exit 1
	}

	print "Generating " $3

	type = $2
	blkfile = file "_" $3

	close(cmnfile)
	copy(cmnfile, blkfile)

	next
}

{ 
	if (common == 1) {
		print $0 >> cmnfile
	} else {
		print $0 >> blkfile
	}
}

END {
	compile(blkfile, type)

        if(blkfile != "") {
                print "#endif" >> file ".h"
        }

	remove(cmnfile)
}
